#! /usr/local/bin/rexx

/******************************************************************************/
/* FIND BOOKS:                                                                */
/*                                                                            */
/*      This program illustrates basic arrays by retrieving book titles       */
/*      based on keyword weightings.                                          */
/******************************************************************************/

/* Initialize both arrays to all null string                                  */
keyword. = ''
title.   = ''

/* The array of keywords to search for among the book descriptors             */
keyword.1 = "earth"
keyword.2 = "computers"
keyword.3 = "life"
keyword.4 = "environment"

/* The array of book titles, each having 3 descriptors                        */
title.0 = 3
title.1 = "Saving Planet Earth"
  title.1.1 = "earth"
  title.1.2 = "environment"
  title.1.3 = "life"
title.2 = "Computer Lifeforms"
  title.2.1 = "life"
  title.2.2 = "computers"
  title.2.3 = "intelligence"
title.3 = "Algorithmic Insanity"
  title.3.1 = "computers"
  title.3.2 = "algorithms"
  title.3.3 = "programming"
title.4 = "Rexx Fun!"
  title.4.1 = "programming"
  title.4.2 = "learning"
  title.4.3 = "enjoyment"

/* Get number keyword matches required for retrieval                          */
arg weight .

say "For weight of" weight "retrieved titles are:"           /* Output header */

do j = 1 while title.j <> ''                             /* Look at each book */
  count = 0
  do k = 1 while keyword.k <> ''                       /* Inspect its keyword */
    do l = 1 while title.j.l <> ''
      if keyword.k = title.j.l then count = count + 1
    end /* Do */
  end /* Do */

  if count >= weight then
    say title.j
end /* Do */

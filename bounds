/* Start the game...                                                          */
say
say "This is the number guessing game called Bounds!"

/* Wrapper for continuing play                                                */
do until continue_playing == "NO"
  guesses = 0
  upper_bounds  = random(10,1000)
  lower_bounds  = upper_bounds - random(9,upper_bounds-1)

  /* First guess:                                                             */
  say
  say "Pick a number between" lower_bounds "and" upper_bounds "for me to guess..."

  do until ready_to_play == "YES"
    say "Have you picked a number?"
    parse upper pull ready_to_play .
    say
  end /* Do */


  first_guess   = trunc((upper_bounds + lower_bounds)/2)
  say "My first guess is: " first_guess
  say

  /* The meat of the action:                                                  */
  do until user_answer == "CORRECT" | user_answer == "YES"
    parse upper pull user_answer .
    say

    select
      /* If the user says LARGER, then adjust the lower_bounds to be one       *
      *  number greater than the difference of the upper and lower bounds.     *
      *                                                                        *
      *  If the user says SMALLER, then adjust the upper_bounds to be one      *
      *  number less than the difference of the upper and lower bounds.        *
      *                                                                        *
      *  Then guess again...                                                  */

      when user_answer == "LARGER" | user_answer == "HIGHER" then do
          /* The lower_bounds should always be smaller than the upper_bounds. */
          lower_bounds = min(trunc((upper_bounds + lower_bounds) / 2),upper_bounds)
          guesses = guesses + 1
          say "Is the number: " trunc((upper_bounds + lower_bounds) / 2)
          say
        end /* When */

      when user_answer == "SMALLER" | user_answer == "LOWER" then do
          /* The upper_bounds should always be larger than the lower_bounds.  */
          upper_bounds = max(trunc((upper_bounds + lower_bounds) / 2),lower_bounds)
          guesses = guesses + 1
          say "Is the number: " trunc((upper_bounds + lower_bounds) / 2)
          say
        end /* When */

      when user_answer == "CORRECT" | user_answer == "YES" then do
          guesses = guesses + 1
          say
          say "Yes! I did it!"
          say "It took me" guesses "guesses..."
          say "But I did it!"
          say
        end /* When */

      otherwise do
          say
          say "Invalid response."
          say "Please resubmit your answer:"
        end /* Otherwise */
    end /* Select */
  end /* Do */

  /* Continue playing?                                                        */
  say
  say "Wanna play another game of Bounds?"
  say
  parse upper pull continue_playing .
  say

end /* Do */

say "Good-bye...for now!"
say
